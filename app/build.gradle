plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.crashlytics)
    alias(libs.plugins.google.devtools.ksp)
    alias(libs.plugins.android.hilt)
}

android {

    def VERSION_NAME_MAJOR = 3
    def VERSION_NAME_MINOR = 2
    def VERSION_NAME_PATCH = 0
    def VERSION_NAME_BUILD = 0
    def VERSION_CODE = 38

    namespace 'com.gowittgroup.smartassist'
    compileSdk 34


    defaultConfig {
        applicationId "com.gowittgroup.smartassist"
        minSdk 24
        targetSdk 34
        versionCode VERSION_CODE
        versionName VERSION_NAME_MAJOR + "." + VERSION_NAME_MINOR + "." + VERSION_NAME_PATCH + "." + VERSION_NAME_BUILD

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        archivesBaseName = "SmartAssist-v$versionName-$versionCode"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

}

dependencies {
    implementation project(path: ':smartassistlib')

    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.runtime.livedata

    // Compose
    implementation platform(libs.compose.bom)
    androidTestImplementation platform(libs.compose.bom)
    implementation libs.androidx.compose.material.icons.extended
    implementation libs.android.compose.ui.toolingPreview
    debugImplementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.activity.compose
    implementation libs.androidx.composeUi
    implementation libs.androidx.navigation.compose

    // Material Design 3
    implementation libs.androidx.material3
    implementation libs.androidx.material3WindowSizeClass

    // RichText
    implementation libs.richtext.uiMaterial3
    implementation libs.richtext.commonmark
    implementation libs.richtext.ui

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics.ktx
    implementation libs.firebase.crashlytics.ktx
    implementation libs.firebase.config.ktx
    implementation libs.firebase.messaging.ktx

    //Hilt
    implementation libs.hilt.android
    implementation libs.androidx.hilt.navigation.compose
    ksp libs.hilt.compiler

    //Lottie
    implementation libs.lottie.compose

    //Leak Canary
    debugImplementation libs.leakcanary.android

    //Review
    implementation libs.playstore.review
    implementation libs.playstore.reviewktx
    implementation libs.androidx.core.splashscreen

    // Test
    testImplementation libs.junit.jupiter
    androidTestImplementation libs.androidx.junit
    testImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.androidx.espresso.core

    // UI Tests
    androidTestImplementation libs.androidx.compose.ui.test.manifest
    debugImplementation libs.androidx.compose.ui.test.junit4
}