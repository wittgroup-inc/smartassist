plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.crashlytics)
    alias(libs.plugins.google.devtools.ksp)
    alias(libs.plugins.android.hilt)
}

android {

    def VERSION_NAME_MAJOR = 4
    def VERSION_NAME_MINOR = 1
    def VERSION_NAME_PATCH = 0
    def VERSION_NAME_BUILD = 0
    def VERSION_CODE = 50

    namespace 'com.gowittgroup.smartassist'
    compileSdk 35

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        prod {
            dimension "environment"
        }
    }

    defaultConfig {
        applicationId "com.gowittgroup.smartassist"
        minSdk 24
        targetSdk 35
        versionCode VERSION_CODE
        versionName VERSION_NAME_MAJOR + "." + VERSION_NAME_MINOR + "." + VERSION_NAME_PATCH + "." + VERSION_NAME_BUILD

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        archivesBaseName = "SmartAssist-v$versionName-$versionCode"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.5'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    sourceSets {
        dev {
            res.srcDirs += "src/dev"
        }
        prod {
            res.srcDirs += "src/prod"
        }
    }

    packagingOptions {
        resources {
            excludes += "META-INF/DEPENDENCIES"
            excludes += "META-INF/AL2.0"
            excludes += "META-INF/LGPL2.1"
        }
    }

}

dependencies {
    implementation project(path: ':smartassistlib')
    implementation project(path: ':core')

    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.runtime.livedata

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.androidx.lifecycle.process
    implementation libs.billing.ktx

    androidTestImplementation platform(libs.compose.bom)
    implementation libs.androidx.compose.material.icons.extended
    implementation libs.android.compose.ui.toolingPreview
    debugImplementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.activity.compose
    implementation libs.androidx.composeUi
    implementation libs.androidx.navigation.compose

    // Material Design 3
    implementation libs.androidx.material3
    implementation libs.androidx.material3WindowSizeClass

    // RichText
    implementation libs.richtext.uiMaterial3
    implementation libs.richtext.commonmark
    implementation libs.richtext.ui

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics.ktx
    implementation libs.firebase.crashlytics.ktx
    implementation libs.firebase.config.ktx
    implementation libs.firebase.messaging.ktx

    //Auth
    implementation libs.play.services.auth
    implementation libs.play.services.identity


    //Hilt
    implementation libs.hilt.android
    implementation libs.androidx.hilt.navigation.compose
    ksp libs.hilt.compiler

    //Lottie
    implementation libs.lottie.compose

    //Leak Canary
    debugImplementation libs.leakcanary.android

    //Review
    implementation libs.playstore.review
    implementation libs.playstore.reviewktx
    implementation libs.androidx.core.splashscreen

    //Ads
    implementation libs.play.services.ads

    //Coil
    implementation(libs.coil3.coil.compose)
    implementation(libs.coil.network.okhttp)

    // Google Vision OCR
    implementation(libs.text.recognition)

    // PDF Processing
    implementation(libs.tom.roush.pdfbox.android)
    implementation(libs.play.services.mlkit.document.scanner)

    // Test
    testImplementation libs.mockk
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.kotlinx.coroutines.test

    // UI Tests
    androidTestImplementation libs.androidx.compose.ui.test.manifest
    androidTestImplementation libs.androidx.espresso.core
    debugImplementation libs.androidx.compose.ui.test.junit4
}